// Vyper grammar for Lark

// A module is a sequence of definitions and methods (and comments).
// NOTE: Start symbol for the grammar
// NOTE: Module can start with docstring
module: ( DOCSTRING
        | COMMENT
        | import
        | struct_def
        | interface_def
        | constant_def
        | variable_def
        | event_def
        | method_def
        | _NEWLINE )*


// Import statements (Supports all styles of Python imports)
!dots: "."+
_AS: "as"
_FROM: "from"
_IMPORT: "import"
import_name: NAME ("." NAME)*
import_rename: NAME ("." NAME)* _AS NAME
import_list: (import_name | import_rename) ("," (import_name | import_rename) )* [","]
import_from: _FROM (dots? import_name | dots)
import: ( _IMPORT (import_name | import_rename)
        | import_from _IMPORT ( "*" | import_name | import_rename )
        | import_from _IMPORT "(" import_list ")" )


// Constant definitions
// NOTE: Temporary until decorators used
constant_def: NAME ":" "constant" "(" type ")" "=" _expr

variable: NAME ":" type
// NOTE: Temporary until decorators used
variable_with_getter: NAME ":" "public" "(" type ")"
variable_def: variable | variable_with_getter

// A decorator "wraps" a method, modifying it's context.
// NOTE: One or more can be applied (some combos might conflict)
decorator: "@" NAME [ "(" [arguments] ")" ] _NEWLINE
decorators: decorator+

// Methods take a list of zero or more typed parameters,
// and can return up to one parameter.
// NOTE: Parameters can have a default value,
//       which must be a constant or environment variable.
parameter: NAME ":" type ["=" _expr]
parameters: parameter ("," parameter?)*

_METHOD_DECL: "def"
_RETURN_TYPE: "->"
method_type: _METHOD_DECL NAME "(" [parameters] ")" [_RETURN_TYPE type]
method_def: [decorators] method_type ":" [DOCSTRING] body

indexed_event_arg: NAME ":" "indexed" "(" type ")"
// Events can have zero args
event_def: NAME ":" "event" "(" "{" (variable | indexed_event_arg)? ("," (variable | indexed_event_arg))* "}" ")"


// Types
array_def: (NAME | array_def) "[" (DEC_NUMBER | NAME) "]"
tuple_def: "(" ( NAME | array_def | tuple_def ) ( "," ( NAME | array_def | tuple_def ) )* [","] ")"
// NOTE: Map takes a basic type and maps to another type (can be non-basic, including maps)
_MAP: "map"
map_def: _MAP "(" ( NAME | array_def ) "," type ")"
type: ( NAME | array_def | tuple_def | map_def )

// Structs can be composed of 1+ basic types or other custom_types
_STRUCT_DECL: "struct"
struct_member: NAME ":" type
struct_def: _STRUCT_DECL NAME ":" _NEWLINE _INDENT (struct_member _NEWLINE)+ _DEDENT

// Interfaces are composed of a series of method definitions, plus their mutability
_CONSTANT_METHOD: "constant"
_MUTABLE_METHOD: "modifying"
mutability: _MUTABLE_METHOD -> mutable
          | _CONSTANT_METHOD -> constant
_INTERFACE_DECL: "contract"
interface_def: _INTERFACE_DECL NAME ":" _NEWLINE _INDENT (method_type ":" mutability _NEWLINE)+ _DEDENT


// Statements
_stmt: ( declaration
       | assign
       | aug_assign
       | if_stmt
       | for_stmt
       | return_stmt
       | pass_stmt
       | break_stmt
       | continue_stmt
       | log_stmt
       | raise_stmt
       | assert_stmt
       | _expr ) [COMMENT]

declaration: variable "=" _expr
multiple_assign: (variable_access | "_") ("," (variable_access | "_"))+
assign: (variable_access | multiple_assign | "(" multiple_assign ")" ) "=" _expr
// NOTE: Keep these in sync with bin_op below
?aug_operator: "+"  -> add
             | "-"  -> sub
             | "*"  -> mul
             | "/"  -> div
             | "%"  -> mod
             | "**" -> pow
             | "<<" -> shl
             | ">>" -> shr
             | _AND -> and
             | _OR  -> or
             | _XOR -> xor
aug_assign: variable_access aug_operator "=" _expr

_PASS: "pass"
_BREAK: "break"
_CONTINUE: "continue"
_LOG: "log"
_RETURN: "return"
_RAISE: "raise"
_ASSERT: "assert"

pass_stmt: _PASS
break_stmt: _BREAK
continue_stmt: _CONTINUE

// TODO: Remove "." as unnecessary
log_stmt: _LOG "." NAME "(" [arguments] ")"
return_stmt: _RETURN [_expr ("," _expr)*]
_UNREACHABLE: "UNREACHABLE"
raise_stmt: _RAISE -> raise
          | _RAISE STRING -> raise_with_reason
          | _RAISE _UNREACHABLE -> raise_unreachable
assert_stmt: _ASSERT _expr -> assert
           | _ASSERT _expr "," STRING -> assert_with_reason
           | _ASSERT _expr "," _UNREACHABLE -> assert_unreachable

body: _NEWLINE _INDENT (COMMENT | _NEWLINE | _stmt)+ _DEDENT
if_stmt: "if" _expr ":" body ("elif" _expr ":" body)* ["else" ":" body]
// TODO: make this into a variable definition e.g. `for i: uint256 in range(0, 5): ...`
for_stmt: "for" NAME "in" _expr ":" body


// Expressions
_expr: operation
     | dict

get_item: variable_access "[" _expr "]"
get_attr: variable_access "." NAME
call: variable_access "(" [arguments] ")"
?variable_access: NAME -> get_var
                | get_item
                | get_attr
                | call
                | "(" variable_access ")"

?argument: _expr ["=" _expr]
arguments: argument ("," argument)* [","]

tuple: "(" "," ")" | "(" _expr ( ("," _expr)+ [","] | "," ) ")"
list: "[" "]" | "[" _expr ("," _expr)* [","] "]"
dict: "{" (NAME ":" _expr) ("," (NAME ":" _expr))* [","] "}"


// Operators
// NOTE: The recursive cycle here helps enforce operator precedence
//       Precedence goes up the lower down you go
?operation: bin_op

_POW: "**"
_SHL: "<<"
_SHR: ">>"
// Binary Operations
// NOTE: Keep these in sync with aug_assign above
?bin_op: product
       | bin_op "+"  product -> add
       | bin_op "-"  product -> sub
       | bin_op _SHL product -> shl
       | bin_op _SHR product -> shr
?product: power
        | product "*"   power -> mul
        | product "/"   power -> div
?power: bool_op
      | power _POW  bool_op -> pow
      | power "%"   bool_op -> mod

_AND: "and"
_OR: "or"
_NOT: "not"
_XOR: "xor"
// Boolean Operations
?bool_op: unary_op
        | bool_op _AND unary_op -> and
        | bool_op _OR  unary_op -> or
        | bool_op _XOR unary_op -> xor

// Unary Operations
?unary_op: comparator
         | "+"  unary_op -> uadd
         | "-"  unary_op -> usub
         | _NOT unary_op -> not

// Comparisions
_EQ: "=="
_NE: "!="
_LE: "<="
_GE: ">="
_IN: "in"
?comparator: atom
           | comparator "<" atom ->  lt
           | comparator ">" atom ->  gt
           | comparator _EQ atom ->  eq
           | comparator _NE atom ->  ne
           | comparator _LE atom ->  le
           | comparator _GE atom ->  ge
           | comparator _IN atom ->  in

// NOTE: Must end recursive cycle like this (with `atom` calling `operation`)
?atom: variable_access
     | literal
     | tuple
     | list
     | "(" operation ")"


// Tokens
// Adapted from Lark repo. https://github.com/lark-parser/lark/blob/master/examples/python3.lark
// Adapted from: https://docs.python.org/3/reference/grammar.html
// Adapted by: Erez Shinan
NAME: /[a-zA-Z_]\w*/
COMMENT: /#[^\n]*/
_NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )+


STRING: /b?("(?!"").*?(?<!\\)(\\\\)*?"|'(?!'').*?(?<!\\)(\\\\)*?')/i
DOCSTRING: /(""".*?(?<!\\)(\\\\)*?"""|'''.*?(?<!\\)(\\\\)*?''')/is

DEC_NUMBER: /0|[1-9]\d*/i
HEX_NUMBER.2: /0x[\da-f]*/i
OCT_NUMBER.2: /0o[0-7]*/i
BIN_NUMBER.2 : /0b[0-1]*/i
FLOAT_NUMBER.2: /((\d+\.\d*|\.\d+)(e[-+]?\d+)?|\d+(e[-+]?\d+))/i

_number: DEC_NUMBER
       | HEX_NUMBER
       | BIN_NUMBER
       | OCT_NUMBER
       | FLOAT_NUMBER

BOOL: "True" | "False"

// TODO: Remove Docstring from here
literal: ( _number | STRING | DOCSTRING | BOOL )

%ignore /[\t \f]+/  // WS
%ignore /\\[\t \f]*\r?\n/   // LINE_CONT
%ignore COMMENT
%declare _INDENT _DEDENT
